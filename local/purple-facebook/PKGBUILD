# Maintainer: Balló György <ballogyor+arch at gmail dot com>

pkgname=purple-facebook
pkgver=0.9.6.r8.g1a6711f
pkgrel=1
pkgdesc='Facebook protocol plugin for libpurple'
arch=('x86_64')
url='https://github.com/dequis/purple-facebook'
license=('GPL')
depends=('json-glib' 'libpurple')
makedepends=('mercurial' 'autoconf' 'git')
source=("$pkgname::git+https://github.com/dequis/purple-facebook"
        "pidgin::hg+https://keep.imfreedom.org/pidgin/pidgin")
sha256sums=('SKIP'
            'SKIP')

pkgver() {
  cd $pkgname
  git describe --long --tags | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
  cd $pkgname
  cp -r $srcdir/pidgin .pidgin
}

build() {
  cd $pkgname
  ./autogen.sh --prefix=/usr
  make
}

package() {
  cd $pkgname
  make DESTDIR="$pkgdir" install
}
