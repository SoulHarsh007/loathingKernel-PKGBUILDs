# Maintainer: Asa Venton <asav1410 at gmail dot com>
# Contributor: onny <onny@project-insanity.org>
# Contributor: Jakob Nixdorf <flocke@shadowice.org>
# Maintainer: Vin√≠cius dos Santos Oliveira <vini.ipsmaker@gmail.com>

pkgname=pidgin-opensteamworks
pkgver=1.7.2.r0.g4274774
pkgrel=1
pkgdesc="A plugin to connect to Steam Friends/Steam IM for libpurple/Pidgin/Adium."
url="https://github.com/EionRobb/${pkgname}"
arch=('i686' 'x86_64')
license=('GPL3')
makedepends=('git' 'nss' 'libgnome-keyring')
depends=('libpurple' 'json-glib')
source=("${pkgname}::git+https://github.com/EionRobb/${pkgname}.git")
sha512sums=('SKIP')

pkgver() {
  cd "${pkgname}/steam-mobile"
  git describe --long --tags | sed 's/\([^-]*-g\)/r\1/;s/-/./g'
}

build() {
  cd "${pkgname}/steam-mobile"
  make
}

package() {
  cd "${pkgname}/steam-mobile"
  #installing purple plugin
  install -Dm755 libsteam.so "${pkgdir}/usr/lib/purple-2/libsteam.so"
  #installing icons
  mkdir -p "${pkgdir}/usr/share/pixmaps/pidgin/protocols"
  for size in 16 22 48; do
    install -D steam${size}.png "${pkgdir}/usr/share/pixmaps/pidgin/protocols/${size}/steam.png"
  done
}
