# Maintainer: schuay <jakob.gruber@gmail.com>
# Maintainer: Maxime Gauduin <alucryd@archlinux.org>
# Contributor: Jeremy Newton (Mystro256) <alexjnewt@gmail.com>

pkgbase=dolphin-emu-git
pkgname=(
  dolphin-emu-git
  dolphin-emu-tool-git
)
pkgver=5.0.r21264.1efda863e4
pkgrel=1
epoch=1
pkgdesc='A Gamecube and Wii emulator'
arch=(x86_64)
url=https://dolphin-emu.org
license=(GPL2)
depends=(
  alsa-lib
  bluez-libs
  bzip2
  #enet
  gcc-libs
  glibc
  hidapi
  libavcodec.so
  libavformat.so
  libavutil.so
  libcurl.so
  libevdev
  #libfmt.so
  libgl
  #libmgba
  libpulse
  libsfml-network.so
  libsfml-system.so
  libspng.so
  libswscale.so
  libudev.so
  libusb-1.0.so
  libx11
  libxi
  libxrandr
  lzo
  mbedtls2
  #minizip-ng
  pugixml
  sfml
  speexdsp
  xz
  zstd
)
makedepends=(
  cmake
  git
  miniupnpc
  ninja
  python
  qt6-base
  qt6-svg
)
optdepends=('pulseaudio: PulseAudio backend')
options=(!emptydirs !lto)
_commit=1efda863e47b690f460f069502a4391b3c7d87c4
source=(
  dolphin-emu::git+https://github.com/dolphin-emu/dolphin.git
)
b2sums=('SKIP'
)

prepare() {
  cd dolphin-emu
  git remote set-url origin https://github.com/dolphin-emu/dolphin.git
  git submodule update --init --filter=tree:0 --recursive
}

pkgver() {
  cd dolphin-emu
  git describe | sed 's/-/.r/; s/-g/./'
}

build() {
  export CFLAGS+=' -I/usr/include/mbedtls2'
  export CXXFLAGS+=' -I/usr/include/mbedtls2'
  export LDFLAGS+=' -L/usr/lib/mbedtls2'
  cmake -S dolphin-emu -B build -G Ninja \
    -DCMAKE_BUILD_TYPE=None \
    -DCMAKE_INSTALL_PREFIX=/usr \
    -DDISTRIBUTOR=archlinux.org \
    -DENABLE_TESTS=OFF \
    -DUSE_MGBA=ON \
    -DUSE_SYSTEM_FMT=OFF \
    -DENABLE_AUTOUPDATE=OFF \
    -Wno-dev
  cmake --build build
}

package_dolphin-emu-git() {
  depends+=(
    hicolor-icon-theme
    libminiupnpc.so
    qt6-base
    qt6-svg
  )
  provides=(dolphin-emu)
  conflicts=(dolphin-emu)

  DESTDIR="${pkgdir}" cmake --install build
  install -Dm 644 dolphin-emu/Data/51-usb-device.rules -t "${pkgdir}"/usr/lib/udev/rules.d/
  rm -rf "${pkgdir}"/usr/{bin/dolphin-tool,include,lib/libdiscord-rpc.a}
}

package_dolphin-emu-tool-git() {
  provides=(dolphin-emu-tool)
  conflicts=(dolphin-emu-tool)
  install -Dm 755 build/Binaries/dolphin-tool -t "${pkgdir}"/usr/bin/
}

# vim: ts=2 sw=2 et:
