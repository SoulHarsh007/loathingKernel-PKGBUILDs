# Maintainer: Malah <malah@neuf.fr>
# Contributor: kaptoxic

pkgname=purple-discord
pkgver=0.r867.f8b0c59
pkgrel=1
pkgdesc='A libpurple/Pidgin plugin for Discord'
url='https://github.com/EionRobb/purple-discord'
arch=('x86_64')
license=('GPL3')
makedepends=('git' 'imagemagick' 'librsvg' 'gettext')
depends=('libpurple' 'json-glib' 'qrencode')
source=('purple-discord::git+https://github.com/EionRobb/purple-discord.git'
        'server-grouping.patch'
)
sha512sums=('SKIP'
            '32dbb92ca5195765e9ab70826b56c2eeb3673d8244b671da99c64b5ad833cc4b8971b5ab27d46dacb0b38f1a2e8634d6af1c62fa3f77feb3cdc2010e0583fd11')

pkgver() {
  cd "$pkgname"
  printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd "$pkgname"
  patch -p1 -i "$srcdir"/server-grouping.patch
}

build() {
  cd "$pkgname"
  make
}

package() {
  cd "$pkgname"
  make DESTDIR="$pkgdir" install
  install -Dm644 LICENSE -t "$pkgdir"/usr/share/licenses/"$pkgname"/
}
