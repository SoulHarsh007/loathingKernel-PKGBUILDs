# Contributor: katt <magunasu.b97@gmail.com>

pkgname=duckstation-git
pkgver=0.1.r8059.g3670c13
pkgdesc='A Sony PlayStation (PSX) emulator, focusing on playability, speed, and long-term maintainability (git version)'
pkgrel=1
arch=(x86_64 aarch64)
url=https://github.com/stenzek/duckstation
license=(CC-BY-NC-ND-4.0)
depends=(
    sh
    glibc
    gcc-libs
    sdl2
    qt6-base
    libwebp.so
    libjpeg.so
    libpng16.so
    libxrandr
    libx11
    freetype2 libfreetype.so
    libzip.so
    libzstd.so
    libz.so
    libdbus-1.so
    libcurl.so
    systemd-libs libudev.so
    hicolor-icon-theme
    ffmpeg
    libglvnd
    glslang
    spirv-tools
)
makedepends=(
    git
    cmake
    clang
    lld
    llvm
    extra-cmake-modules
    qt6-tools
    qt6-wayland
    libdrm
    libpulse
    alsa-lib
    sndio
    ninja
    jack
    python
    spirv-headers
)
optdepends=(
    'libdrm'
    'qt6-wayland: Wayland support'
    'libpulse: Pulseaudio support'
    'sndio: Small audio support'
    'alsa-lib: ALSA support'
    'jack: JACK support'
)
provides=("${pkgname%-git}")
conflicts=("${pkgname%-git}")
options=(!lto)

source=(
    git+https://github.com/stenzek/duckstation.git
)
sha256sums=(
    'SKIP'
)

pkgver() {
    cd "${srcdir}/${pkgname%-git}"
    git describe --long --abbrev=7 | sed 's/^v//;s/\([^-]*-g\)/r\1/;s/-/./g'
}

prepare() {
    cd "${srcdir}/${pkgname%-git}"
}

build() {

    ./duckstation/scripts/deps/build-dependencies-linux.sh \
      -system-freetype -system-harfbuzz -system-libjpeg \
      -system-libpng -system-libwebp -system-libzip \
      -system-zstd -system-qt \
      "$PWD"/build-deps

    cmake -B build -S duckstation \
        -G Ninja \
        -Wno-dev \
        -DCMAKE_BUILD_TYPE=Release \
        -DCMAKE_C_COMPILER=clang \
        -DCMAKE_CXX_COMPILER=clang++ \
        -DCMAKE_EXE_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_MODULE_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_SHARED_LINKER_FLAGS_INIT="-fuse-ld=lld" \
        -DCMAKE_INTERPROCEDURAL_OPTIMIZATION=ON \
        -DCMAKE_PREFIX_PATH="$PWD"/build-deps \
        -DALLOW_INSTALL=ON \
        -DINSTALL_SELF_CONTAINED=ON \
        -DCMAKE_INSTALL_PREFIX="${pkgdir}"/opt/duckstation
    ninja -C build -v
}

package() {
    ninja -C build install
    install -Dm755 duckstation/scripts/packaging/duckstation-qt "${pkgdir}/usr/bin/duckstation-qt"
    install -Dm644 duckstation/scripts/packaging/org.duckstation.DuckStation.desktop "${pkgdir}/usr/share/applications/org.duckstation.DuckStation.desktop"
    install -Dm644 duckstation/scripts/packaging/org.duckstation.DuckStation.png "${pkgdir}/usr/share/icons/hicolor/512x512/apps/org.duckstation.DuckStation.png"
    install -Dm644 duckstation/LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
    install -Dm644 duckstation/data/resources/thirdparty.html "${pkgdir}/usr/share/licenses/${pkgname}/thirdparty.html"
}

